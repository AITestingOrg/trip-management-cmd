buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
		verifierVersion = '1.2.3.RELEASE'
		axonVersion = '3.1.2'
		springfoxVersion = '2.7.0'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${verifierVersion}"
	}
}
plugins {
	id "au.com.dius.pact" version "3.5.13"
	id "com.wiredforcode.spawn" version "0.8.2"
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'groovy'
apply plugin: 'spring-cloud-contract'
apply plugin: "jacoco"
apply plugin: 'checkstyle'

group = 'org.aitesting.microservices'
version = '0.0.1'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
	springCloudVersion = 'Edgware.SR2'
}

dependencies {
	// TODO local files dependencies while things are not up.
	compile files('libs/library--axon-configuration-0.0.1.jar')
	compile files('libs/trip-management-common-1.0.jar')

	compile("org.axonframework:axon-core:${axonVersion}")
	compile("org.axonframework:axon-mongo:${axonVersion}")
	compile("org.axonframework:axon-spring-boot-starter:${axonVersion}")
	compile("org.axonframework:axon-spring-boot-autoconfigure:${axonVersion}")
	compile("org.axonframework:axon-amqp:${axonVersion}")
	compile("org.axonframework:axon-test:${axonVersion}")
	compile("io.springfox:springfox-swagger2:${springfoxVersion}")
	compile("io.springfox:springfox-swagger-ui:${springfoxVersion}")

	compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
	compile('org.springframework.cloud:spring-cloud-starter-ribbon')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-amqp')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile group: 'junit', name: 'junit', version: '4.12'

	testCompile 'org.codehaus.groovy:groovy-all:2.4.6'
	testCompile "org.springframework.cloud:spring-cloud-starter-contract-verifier:${verifierVersion}"
	testCompile "org.springframework.cloud:spring-cloud-contract-spec:${verifierVersion}"
	testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

contracts {
	baseClassMappings {
		baseClassMapping(".*tripmanagement.*", "org.aitesting.microservices.tripmanagement.cmd.provider.TripAggregateContractBase")
	}
}

checkstyle {
	toolVersion = "8.7"
	configFile = new File(rootDir, "checkstyle.xml")
	maxWarnings = 0
	maxErrors = 0
}

checkstyleMain {
	source = fileTree('src/main') {
		excludes = ['*.yml']
	}
}
checkstyleTest {
	source = fileTree('src/test') {
		excludes = ['*.yml']
	}
}

jacocoTestReport {
	reports {
		html.destination file("${buildDir}/jacocoHtml")
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.85
			}
		}
	}
}
test.finalizedBy(jacocoTestCoverageVerification)
